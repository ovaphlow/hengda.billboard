plugins {
  id 'application'
  id 'com.google.protobuf' version '0.8.13' //Use plugin version 0.8.10 with earlier than gradle 5.6 versions
  id 'idea'
}

//sourceCompatibility = 1.8

repositories {
  jcenter()
  mavenCentral()
}

def grpcVersion = '1.33.1'
def protobufVersion = '3.12.0'
def protocVersion = protobufVersion

dependencies {
  implementation "io.grpc:grpc-protobuf:${grpcVersion}"
  implementation "io.grpc:grpc-stub:${grpcVersion}"
  compileOnly "org.apache.tomcat:annotations-api:6.0.53"//necessary for Java 9+

  implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

  runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

  implementation "ch.qos.logback:logback-classic:1.2.3"

  implementation "com.zaxxer:HikariCP:3.4.5"
  implementation "mysql:mysql-connector-java:8.0.22"
  implementation 'commons-dbutils:commons-dbutils:1.7'
}

protobuf {
  protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
  plugins {
    grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
  }
  generateProtoTasks {
    all()*.plugins { grpc {} }
  }
}

sourceSets {
  main {
    java {
      srcDirs 'build/generated/source/proto/main/grpc'
      srcDirs 'build/generated/source/proto/main/java'
    }
  }
}

startScripts.enabled = false

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

task bizService(type: CreateStartScripts) {
  mainClassName = 'hengda.billboard.BizService'
  applicationName = 'hengda.billboard.biz-service.202002-11'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
  from(bizService)
  fileMode = 0755
}
